plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "1.2.1"
}

configurations {
    bundledJars {
        canBeConsumed = false
        canBeResolved = true
    }
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    bundledJars(project(path: ":forgeapi", configuration: "instrumentedJars")) { transitive false }
    bundledJars(project(path: ":forgeloader", configuration: "instrumentedJars")) { transitive false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

remapJar {
    input.set(jar.archiveFile)
    dependsOn jar
    classifier "remap"
}

jar {
    archivesBaseName = rootProject.archives_base_name+"-forge"
    manifest {
        attributes([
                "FMLModType": "LIBRARY",
                "Automatic-Module-Name": "clojureunwrapper"
        ])
    }
    classifier "dev"
}

task outputJar (type: Jar) {
    dependsOn remapJar
    duplicatesStrategy 'exclude'
    from remapJar.archiveFile.map { project.zipTree(it) }
    manifest {
        attributes([
                "FMLModType": "LIBRARY",
                "Automatic-Module-Name": "clojureunwrapper"
        ])
    }
    into('jars') {
        from configurations.bundledJars
        rename '(.*)forgeapi(.*)', 'wrapper.jar'
        rename '(.*)forgeloader(.*)', 'loader.jar'
    }
    classifier null
}

sourcesJar {
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

build.dependsOn outputJar

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

curseforge {
    if (System.getenv('CURSEFORGE_KEY') != null) {
        apiKey = System.getenv('CURSEFORGE_KEY')
        project {
            id = '591639'
            changelog = 'See: https://github.com/lukebemish/clojurewrapper/blob/main/changelogs/CHANGELOG.md'
            releaseType = 'beta'
            addGameVersion '1.18.2'
            addGameVersion 'Forge'
            mainArtifact(outputJar) {
                displayName = "ClojureWrapper [Forge 1.18.2] v$project.version"
            }
        }
    }
    options {
        forgeGradleIntegration = false
    }
}
afterEvaluate {
    tasks.curseforge.dependsOn build
}