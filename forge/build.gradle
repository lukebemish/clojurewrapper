plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "1.2.1"
}

configurations {
    bundledJars {
        canBeConsumed = false
        canBeResolved = true
    }
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    bundledJars(project(path: ":forgeapi", configuration: "instrumentedJars")) { transitive false }
    bundledJars(project(path: ":forgeloader", configuration: "instrumentedJars")) { transitive false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

remapJar {
    input.set(jar.archiveFile)
    dependsOn jar
    classifier null
}

jar {
    archivesBaseName = rootProject.archives_base_name+"-forge"
    manifest {
        attributes([
                "FMLModType": "LANGPROVIDER",
                "Automatic-Module-Name": "clojureunwrapper"
        ])
    }
    into('jars') {
        from configurations.bundledJars
        rename '(.*)forgeapi(.*)', 'wrapper.jar'
        rename '(.*)forgeloader(.*)', 'loader.jar'
    }
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

curseforge {
    if (System.getenv('CURSEFORGE_KEY') != null) {
        apiKey = System.getenv('CURSEFORGE_KEY')
        project {
            id = 'project_id'
            changelog = 'See: https://github.com/lukebemish/clojurewrapper/blob/main/changelogs/CHANGELOG.md'
            releaseType = 'beta'
            addGameVersion '1.18.1'
            addGameVersion 'Forge'
            mainArtifact(remapJar) {
                displayName = "ClojureLoader [Forge] v$project.version"
                relations {
                    requiredDependency 'architectury-forge'
                }
            }
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

import com.modrinth.minotaur.TaskModrinthUpload

task modrinth (type: TaskModrinthUpload){ // Make sure it runs after build!
    onlyIf {
        System.getenv("MODRINTH_KEY") // Only attempt to run this task if the MODRINTH variable is set, otherwise SKIP it
    }

    token = System.getenv("MODRINTH_KEY") // An environment property called MODRINTH that is your token, set via Gradle CLI, GitHub Actions, Idea Run Configuration, or other
    projectId = 'project_id'
    versionNumber = "$project.version-forge" // Will fail if Modrinth has this version already
    // On fabric, use 'remapJar' instead of 'jar'
    versionType = "BETA"
    detectLoaders = false

    uploadFile = remapJar // This is the java jar task. If it can't find the jar, try 'jar.outputs.getFiles().asPath' in place of 'jar'
    addGameVersion('1.18.1') // Call this multiple times to add multiple game versions. There are tools that can help you generate the list of versions
    addLoader('forge')
}

afterEvaluate {
    tasks.curseforge.dependsOn build
    tasks.modrinth.dependsOn build
}