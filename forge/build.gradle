plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "1.2.1"
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
    shadow
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    shadow "org.clojure:clojure:${rootProject.clojure_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    configurations = [project.configurations.shadowCommon]
    duplicatesStrategy 'include'
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "remap"
}

task outputJar (type: Jar) {
    dependsOn remapJar
    manifest.from {
        resources.text.fromArchiveEntry(remapJar.archiveFile, "META-INF/MANIFEST.MF")
    }
    manifest {
        attributes([
            "FMLModType": "LANGPROVIDER",
            "Automatic-Module-Name": "clojurewrapper"
        ])
    }
    from remapJar.archiveFile.map { project.zipTree(it) }
    configurations.shadow.each { dep ->
        from(project.zipTree(dep)) {
            //exclude 'META-INF', 'META-INF/**'
        }
    }
    classifier null
}

build.dependsOn outputJar

jar {
    archivesBaseName = rootProject.archives_base_name+"-forge"
    manifest {
        attributes([
                "FMLModType": "LANGPROVIDER",
                "Automatic-Module-Name": "clojurewrapper"
        ])
    }
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

curseforge {
    if (System.getenv('CURSEFORGE_KEY') != null) {
        apiKey = System.getenv('CURSEFORGE_KEY')
        project {
            id = 'project_id'
            changelog = 'See: https://github.com/lukebemish/clojurewrapper/blob/main/changelogs/CHANGELOG.md'
            releaseType = 'beta'
            addGameVersion '1.18.1'
            addGameVersion 'Forge'
            mainArtifact(outputJar) {
                displayName = "ClojureWrapper [Forge] v$project.version"
                relations {
                    requiredDependency 'architectury-forge'
                }
            }
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

import com.modrinth.minotaur.TaskModrinthUpload

task modrinth (type: TaskModrinthUpload){ // Make sure it runs after build!
    onlyIf {
        System.getenv("MODRINTH_KEY") // Only attempt to run this task if the MODRINTH variable is set, otherwise SKIP it
    }

    token = System.getenv("MODRINTH_KEY") // An environment property called MODRINTH that is your token, set via Gradle CLI, GitHub Actions, Idea Run Configuration, or other
    projectId = 'project_id'
    versionNumber = "$project.version-forge" // Will fail if Modrinth has this version already
    // On fabric, use 'remapJar' instead of 'jar'
    versionType = "BETA"
    detectLoaders = false

    uploadFile = outputJar // This is the java jar task. If it can't find the jar, try 'jar.outputs.getFiles().asPath' in place of 'jar'
    addGameVersion('1.18.1') // Call this multiple times to add multiple game versions. There are tools that can help you generate the list of versions
    addLoader('forge')
}

afterEvaluate {
    tasks.curseforge.dependsOn build
    tasks.modrinth.dependsOn build
}
